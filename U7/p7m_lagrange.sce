function y = Lk(x,k)
    // Description of Lk(x,k)
    //  x: vector de nodos
    //  k: Ã­ndice del nodo

    n = length(x);
    r = [x(1:k-1) x(k+1:n)]; // nodos sin x(k)
    p = poly(r,"x","roots"); // Numerador de Lk
    pk = horner(p,x(k));     // Denominador de Lk
    y = p / pk;
endfunction

function pol = lagrange(x, y)
    // Description of lagrange(x, y))
    //  x: vector de nodos
    //  y: vector de valores en los nodos

    n = length(x);
    pol = 0;
    for k = 1:n
        L = Lk(x,k);
        pol = pol + (y(k) * L);
    end
endfunction

// Ejemplo
x = [ 0 1 2]
y = [ -1 -1 7 ]

p = lagrange(x, y)
disp(p)